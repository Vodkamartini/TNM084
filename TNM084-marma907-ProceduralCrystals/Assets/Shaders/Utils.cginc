			// From scalar input, generate scalar white noise
float rand1dTo1d(float3 value, float mutator = 0.546)
{
    float random = frac(sin(value + mutator) * 143758.5453);
    return random;
}

			// From scalar input, generate 3D white noise
float3 rand1dTo3d(float value)
{
    return float3(
					rand1dTo1d(value, 3.9812),
					rand1dTo1d(value, 7.1536),
					rand1dTo1d(value, 5.7241)
					);
}

			// From 3D input, generate scalar white noise
float rand3dTo1d(float3 value, float3 dotDir = float3(12.9898, 78.233, 37.719))
{
				//make value smaller to avoid artefacts
    float3 smallValue = sin(value);
				//get scalar value from 3d vector
    float random = dot(smallValue, dotDir);
				//make value more random by making it bigger and then taking the factional part
    random = frac(sin(random) * 143758.5453);
    return random;
}

			// From 3D input, generate 3D white noise
float3 rand3dTo3d(float3 value)
{
    return float3(
					rand3dTo1d(value, float3(12.989, 78.233, 37.719)),
					rand3dTo1d(value, float3(39.346, 11.135, 83.155)),
					rand3dTo1d(value, float3(73.156, 52.235, 09.151))
					);
}

float3 voronoiNoise(float3 value)
{
    float3 baseCell = floor(value);

	// First pass finds the closest cell
    float minDistToCell = 10;
    float3 toClosestCell;
    float3 closestCell;
	[unroll]
    for (int x1 = -1; x1 <= 1; x1++)
    {
		[unroll]
        for (int y1 = -1; y1 <= 1; y1++)
        {
			[unroll]
            for (int z1 = -1; z1 <= 1; z1++)
            {
                float3 cell = baseCell + float3(x1, y1, z1);
                float3 cellPosition = cell + rand3dTo3d(cell);
                float3 toCell = cellPosition - value;
                float distToCell = length(toCell);
                if (distToCell < minDistToCell)
                {
                    minDistToCell = distToCell;
                    closestCell = cell;
                    toClosestCell = toCell;
                }
            }
        }
    }

	// Second pass finds the distance to the closest edge
    float minEdgeDistance = 10;
	[unroll]
    for (int x2 = -1; x2 <= 1; x2++)
    {
		[unroll]
        for (int y2 = -1; y2 <= 1; y2++)
        {
			[unroll]
            for (int z2 = -1; z2 <= 1; z2++)
            {
                float3 cell = baseCell + float3(x2, y2, z2);
                float3 cellPosition = cell + rand3dTo3d(cell);
                float3 toCell = cellPosition - value;

                float3 diffToClosestCell = abs(closestCell - cell);
                bool isClosestCell = diffToClosestCell.x + diffToClosestCell.y + diffToClosestCell.z < 0.1;
                if (!isClosestCell)
                {
                    float3 toCenter = (toClosestCell + toCell) * 0.5;
                    float3 cellDifference = normalize(toCell - toClosestCell);
                    float edgeDistance = dot(toCenter, cellDifference);
                    minEdgeDistance = min(minEdgeDistance, edgeDistance);
                }
            }
        }
    }

    float random = rand3dTo1d(closestCell);
    return float3(minDistToCell, random, minEdgeDistance);
}